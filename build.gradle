buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
        classpath ('org.springframework:springloaded:1.2.6.RELEASE')
    }
}

allprojects {
    group 'com.github.springbootdubboxdemo'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public" }
    }

    ext {
        springVersion = '4.3.2.RELEASE'
        springBootVersion = '1.4.0.RELEASE'
        springfoxVersion = '2.5.0'
    }

    configurations {
        compile.exclude group:'log4j'
        compile.exclude group:'org.slf4j', module:'slf4j-log4j12'
        compile.exclude group:'commons-logging'
        compile.exclude group:'org.apache.httpcomponents'
        compile.exclude group:'com.google.guava', module:'guava'
        compile.exclude group:'org.springframework', module: 'spring-web'
    }
}

project(":dubbo-starter") {
    dependencies {
        compile("com.alibaba:dubbo:2.8.4"){
            exclude module:'aopalliance:aopalliance'
            exclude module:'javax.servlet-api:javax.servlet'
            exclude module:'httpclient:org.apache.httpcomponents'
            exclude module:'commons-pool:commons-pool'
            exclude module:'commons-codec:commons-codec'
            exclude module:'httpcore:org.apache.httpcomponents'
            exclude module:'org.apache.curator'
        }
        compile ("org.springframework.boot:spring-boot-starter:${springBootVersion}")
        compile("org.apache.zookeeper:zookeeper:3.4.9")
    }
}

configure([project(':order-center'), project(':user-center')]) {
    dependencies {
        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
        compile(project(":api"))
        compile(project(":dubbo-starter"))
        compile("com.101tec:zkclient:0.9")
        compile("ma.glasnost.orika:orika-core:1.5.0")
        compile("javax.validation:validation-api:1.1.0.Final")
        compile("org.hibernate:hibernate-validator:5.3.4.Final")
    }
}